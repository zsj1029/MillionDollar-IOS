import console;
import process;
import process.popen;
import fsys;
import fsys.file;
import fsys.stream;
import gdip;
import web.json;
import winex;
import web.rest.client;
import crypt.bin; 
import string.xml;
import web.mshtml;
import inet.url;
import string.html;
import thread.event;
import thread.manage;
import time.performance;

console.setTitle("答题辅助工具IOS");
var imgpath = "C:\screenshot\screenshot.png";
var keypath = "C:\screenshot\token.txt";
var api = "http://text.aliapi.hanvon.com/rt/ws/v1/ocr/text/recg?code=74e51a88-41ec-413e-b162-bd031fe0407e";
var appcode = "xxxxx";
var bdorcapi = "https://aip.baidubce.com/rest/2.0/ocr/v1/general_basic";
var bdorcapi1 ='https://aip.baidubce.com/rest/2.0/ocr/v1/webimage'
var bdhighorcapi = 'https://aip.baidubce.com/rest/2.0/ocr/v1/accurate_basic';
var apiKey = "fGRChj2rPl5rPPekw05uGB5y";
var secretKey = "MuLcPEZNnQ6mA3Wx5NPipxbNWXL2F9pR";
var token_url = "https://aip.baidubce.com/oauth/2.0/token";
var bd_token = "";
var window = null;
var now = tonumber(time.now());


//api获取token
var to_auth = function(){
	var apiClient = web.rest.client();
	var params = {
		grant_type = "client_credentials";
		client_id = apiKey;
		client_secret = secretKey;
	}
	var json = apiClient.get(token_url,params);
	var resp = web.json.parse(json);
	resp.expires_in = now + resp.expires_in;
	bd_token = resp.access_token;
	var keyfile = fsys.file(keypath,"w+");
	json = web.json.stringify(resp);
	keyfile.write(json);
	keyfile.close();
	//console.dump(resp);
}


//处理过期token
var get_auth = function(){
    if(!fsys.searchFile(keypath))
	{
		to_auth();
    }else {
        var keyfile = fsys.file(keypath,"r+");
        var json = keyfile.readAll();
        var resp = web.json.parse(json);
    	if(resp.expires_in == null || now > resp.expires_in){
    		to_auth();
    	}else {
    		bd_token = resp.access_token;
    	}
    	keyfile.close();
    }
}


//IOS截屏
var jieping = function(time){
    imgpath = "C:\screenshot\screenshot"+time+".png"
    //fsys.delete(imgpath);
    if(window==null)
		window = winex.find(,"^Apowersoft苹果录屏[.]*");
		
	if(window==null){
		console.log("请先安装开启Apowersoft苹果录屏软件");
		console.log("");
		return  false; 
	}
	var pic = com.picture.snap(window);
	pic.Save(imgpath);
	
	return  true; 
}

//截屏裁剪
var caijian = function(){
    //980
	var bitmap = gdip.bitmap(imgpath);
	var bitmagNew = gdip.bitmap(370,370);
	var graphics = bitmagNew.getGraphics();
	graphics.drawImageRectRect(bitmap,0,0,380,370,20,130,380,370)
	bitmagNew.save("C:\screenshot\screenshot_block.png")
}

//百度免费每天500orc识别
var bdorc = function(){
	var apiClient = web.rest.client();
	var headers  ={
		['Content-Type'] = "application/json; charset=UTF-8";
		['Content-Type'] = "application/x-www-form-urlencoded";
	}
	var streams = fsys.stream("C:\screenshot\screenshot_block.png","r+");
	var content = streams.readAll();
	var params = {
		access_token = bd_token;
		image = crypt.bin.encodeBase64(content);
	}
	var json = apiClient.post(bdorcapi,params);
	var resp = web.json.parse(json);
	//console.log(json);
	var question = "";
	var answer = {};
	if(resp.words_result==null)
	{
		console.log("识别失败："+resp.result);
		return null; 
	}

	var num = resp.words_result_num;
	var find_ques = false;
	for(key,val in resp.words_result){
		
		if(!find_ques)
		{
			question = question+""+val.words;
			if(string.find(val.words,"?"))//如果找到问号，问题截取ok
			{
				find_ques = true;
			}
		}else {
			val.words = string.replace(val.words,"《","");
			val.words = string.replace(val.words,"》","");
			val.words = string.replace(val.words,"^\a+\p+","");
			table.push(answer,val.words);
		}
	}
	console.log("");
	console.log(question);
	//除去序号1.2.3.
	questions = string.replace(question,"^\d+[.]*","");
	questions = string.replace(questions,"?","");
	//console.log(questions);
	/**
	for( i =12;1;-1){
		if(questions!=question)
			break ;
		questions = string.replace(question,i,"");
	}**/
	
	var obj = {
		question = questions;
		answer = answer;
	}
	return obj; 
}



//orc识别
var orcshibie = function(){
	var apiClient = web.rest.client();
	var headers  ={
		['Content-Type'] = "application/json; charset=UTF-8";
		['Content-Type'] = "application/octet-stream";
		['Authorization'] = "APPCODE " + appcode;
	}
	apiClient.addHeaders = headers;
	var streams = fsys.stream("C:\screenshot\screenshot_block.png","r+");
	var content = streams.readAll();
	var params = {
		uid = "118.12.0.12";
		color = "color";
		lang = "chns";
		image = crypt.bin.encodeBase64(content);
	}
	
	var json = apiClient.post(api,web.json.stringify(params,true));
	var resp = web.json.parse(json);
	//console.dump(resp);
	
	if(resp.code == "0"){
		var question = resp.textResult;
		question = string.replace(question,"\r","");
		question = string.replace(question,"\n","");
		question = string.replace(question," ","");
		console.log(question);
		return question; 
	}else {
		console.log("识别失败："+resp.result)
		return null; 
	}
}

//多线程请求搜索
var search_thread = function(question,answer){
    import inet.url;
    import thread.event;
	import console;
	import web.mshtml;
	import string.html;
	//console.log(question+" "+answer);
	
	var words = string.concat(question," ",answer);
	words = inet.url.encode(words)
	html = web.mshtml();
	html.document.designMode = "on";
	url = "https://www.baidu.com/s?wd="+words;
	html.go(url);
	qEle = html.queryEles(className = "nums");
	var content = qEle[1].innerText;
	var pos1 = string.indexAny(content,"约");
	substr = string.sub(content,pos1);
	substr = string.replace(substr,"约","");
	substr = string.replace(substr,"个","");
	substr = string.replace(substr,",","");
	console.log(answer+"： "+substr/1000+"千");
	//console.dump(pos2);

}

var search = function(obj){
    //console.dump(obj);
    //console.log(obj.question);
	question = obj.question;
	answer = obj.answer
	
	questions = inet.url.encode(question)
	
	html = web.mshtml();
	html.document.designMode = "on";
	url = "https://www.baidu.com/s?wd="+questions;
	html.go(url);
	
	qEle = html.queryEles(className = "c-abstract");
	console.log("--------------------------------------------------------------------------");
	console.log("搜索返回：");
	for(key,val in qEle){
		if(string.find(val.innerText,"最佳答案")){
			console.log(val.innerText);
			console.log("~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~");
		}
	}

	if(string.find(question,"不是")||string.find(question,"不正确")
	||string.find(question,"不含")||string.find(question,"不包")
	||string.find(question,"不属")||string.find(question,"不在")
	||string.find(question,"没有")||string.find(question,"不可能"))
	{
		console.log("*此题可能是否定题，建议选统计次数最少的答案*");
	}else {
		console.log("*建议选统计次数最多的答案*");
	}
	console.log("~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~");
	
	var content = html.html;
	content = string.html.toText(content);
	console.log("统计答案出现次数：")

	for(key,val in answer){
		//console.log(val);
		strlist = string.split(content,string.concat("<",val,">"));
		console.log(string.concat(val," ",(#strlist-1),"个"));
	}
	

	//多线程搜索问题+答案 结果记录数
	console.log("--------------------------------------------------------------------------");
	console.log("统计问题+答案的搜索条数：")
	manage = thread.manage(10)
	for(key,val in answer){
		
		manage.create(search_thread,question,val);
	}
	manage.waitClose();

} 




var init = function(){

	fsys.createDir("c:/screenshot/");
	get_auth();//获取百度token
	window = winex.find(,"^Apowersoft苹果录屏[.]*");

	console.log("按回车键开始识别问题..." );
}

init();

var times = 1;
while(true){ 
	if( console.kbHit() ){
		var kb = console.kbRead();
		if(!kb) continue;
		if( kb.wVirtualKeyCode == 0x1B/*_VK_ESC*/ ){
			break;
		}
		if(kb.bKeyDown==1)
		{
			console.clearScreen();
			var time1 = time.performance.tick();
			rs = jieping(times);
			if(rs){
				times++;
				caijian();
				var question = bdorc();
				if(!question){
					continue ;
				}	
				search(question);
				var time2 = time.performance.tick();
				console.log("--------------------------------------------------------------------------");
				console.log("处理时间："+(time2-time1)/1000+"秒");
				console.log("");
			}
			
			console.log("按回车键开始识别下一道题..." );
		
		}
		 
	}
}


